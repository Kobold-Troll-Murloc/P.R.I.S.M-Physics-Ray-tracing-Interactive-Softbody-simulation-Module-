#version 460
#extension GL_EXT_ray_tracing : require

layout(set = 0, binding = 0) uniform accelerationStructureEXT topLevelAS;
layout(set = 0, binding = 1, rgba8) uniform image2D image;

struct Light {
    vec3 position;
    float intensity;
    vec3 color;
    int enabled;
};

layout(set = 0, binding = 2, std140) uniform UniformBufferObject {
    mat4 viewInverse;
    mat4 projInverse;
    vec3 cameraPos;
    float padding1;
    Light lights[3];
    int   lightCount;
    float padding2[3];
} ubo;

// 공통 payload
layout(location = 0) rayPayloadEXT vec3 hitValue;
layout(location = 1) rayPayloadEXT bool isShadowed;

vec3 generateRayDirection(vec2 ndc)
{
    vec4 clip  = vec4(ndc, 0.0, 1.0);
    vec4 view  = ubo.projInverse * clip;
    view      /= view.w;
    vec4 world = ubo.viewInverse * vec4(view.xyz, 0.0);
    return normalize(world.xyz);
}

void main()
{
    vec2 pixelCenter = vec2(gl_LaunchIDEXT.xy) + vec2(0.5);
    vec2 uv          = pixelCenter / vec2(gl_LaunchSizeEXT.xy);
    vec2 ndc         = uv * 2.0 - 1.0;

    vec3 rayOrigin    = ubo.cameraPos;
    vec3 rayDirection = generateRayDirection(ndc);

    hitValue   = vec3(0.0);
    isShadowed = false; // 기본값

    traceRayEXT(
        topLevelAS,
        gl_RayFlagsOpaqueEXT,
        0xFF,
        0, 0, 0,      // missIndex = 0 -> 환경광
        rayOrigin,
        0.001,
        rayDirection,
        1e16,
        0
    );

    imageStore(image, ivec2(gl_LaunchIDEXT.xy), vec4(hitValue, 1.0));
}
